#!/usr/bin/gnuplot

### Variables to config ###
xmax=495
ymax=13.5
#########################


### Script ###
set encoding utf8
cd 'useable'
set xrange [240:xmax]
set yrange [0:ymax]
#set xlabel "Tage des nach dem 1.1.2011 (Ein Jahr als 360 Tage)"
set xtics ("1.9.2011" 240,  "1.10." 270, "1.11." 300, "1.12." 330, "1.1.2012" 360, "1.2." 390, "1.3." 420, "1.4." 450, "1.5." 480)
#set arrow from 258,0 to 258,ymax nohead # Wahl in berlin
set ylabel "Prozent Stimmen für Piraten laut Umfragen"
set pointsize 0.7
set key below
plot 'allensbach' with lp	ti "Allensbach", \
  'emnid'	with lp		ti "Emnid", \
  'fgw'		with lp		ti "Forsch’gr. Wahlen", \
  'fgw1'	with points	ti "FgW, Stimmung", \
  'forsa'	with lp		ti "Forsa", \
  'gms'		with lp		ti "GMS", \
  'infra'	with lp		ti "Infratest dimap", \
  5		with dots	ti ""
pause 5 #show it for 5 seconds

# Then plot to svg an png-files
set terminal svg
set object 1 rect from screen 0, 0, 0 to screen 1, 1, 0 behind
set object 1 rect fc  rgb "white"  fillstyle solid 1.0
set output '../plot.svg'
replot
set terminal png
set output '../plot.png'
replot

# And plot also zoomed version to svg and png
unset arrow
set terminal svg
set xrange [260:xmax]
set yrange [3:ymax]
set output '../plot-zoomed.svg'
replot
set terminal png
set output '../plot-zoomed.png'
replot
